# $gladiator_fight_start
!START OF THE GLADIATRIX FIGHT
$fight_return = 'Start'
arena_mode = 1

base_str = slave['stamina'] + slave["gladiatrix_affinity"]
if slave['age'] = CONST_INT['loli_age']: base_str = base_str/2
base_flagelation = slave['gladiatrix'] + slave["gladiatrix_affinity"]
сила = slave['gladiatrix'] + slave["gladiatrix_affinity"]
ловкость = slave['gladiatrix'] + slave["gladiatrix_affinity"]
восприятие = slave['gladiatrix'] + slave["gladiatrix_affinity"]
if slave["gladiatrix_affinity_revealed"] = 0: slave["gladiatrix_affinity_revealed"] = 1

!reset vars
поверженные_враги = 0
killvar '$состояние'

killvar 'слабость'
killvar 'скованность'
killvar 'боль'
killvar 'шаткость'
killvar 'ошеломление'
killvar 'рассеянность'
killvar 'бешенство' 
killvar 'разгон'
killvar 'меткость'
killvar 'адреналин'
killvar 'верткость'
killvar 'внимание'

killvar 'суперудар'
killvar 'наложение'
killvar '$боевая_обстановка'
killvar '$действия_игрока'
killvar '$действия_врага'
killvar '$суперудар'  

суперудар_сделан[1] = IIF(slave['gladiatrix'] > 2, 0, 1)

!interface
gs 'раскладка_бой'

!Getting information about the player
gs '#тексты_движка'
gs '#противники'
gs '#суперудары'
gs '#боевые_предметы'
gs '#dynamic'

$combatant_ava = '<<$slave["ava_clear"]>>.jpg'
$combatant_name = $slave_name 
$броня = $armor[$g_type]
$оружие = $style_wpn[$g_type]
$вторичный = $style_freehand[$g_type]

макс_здоровье_игрока = slave["stamina"] * 10 - slave_rate['wounds']
if макс_здоровье_игрока < 1: макс_здоровье_игрока = 1
здоровье_игрока = макс_здоровье_игрока
макс_выносливость_игрока = 50 + (slave["energy"]*5)
выносливость_игрока = макс_выносливость_игрока
норм_дух_игрока = slave["ego"]*10
дух_игрока = норм_дух_игрока
сила_передышки = 10 
slave["calories"] -= 2
slave_rate["hygiene"] += 40
slave_rate["sensitivity"] -= 5

dynamic $броня_надеть[$броня]
dynamic $броня_эффект[$броня]

!Cycle information about mobs-enemies
i = 0
:loop
IF i < количество_врагов:
 i+=1
 $имя_врага[i] = $название_моба[$враг[i]]
 $склонение_врага[i] = $склонение_моба[$враг[i]]
 $класс_врага[i] = $класс_моба[$враг[i]]
 $труп_врага[i] = $смерть[$враг[i]]
 $тело_врага[i] = $отключка[$враг[i]]
 $беглец[i] = $бегство[$враг[i]]

 макс_здоровье_врага[i] = базовое_здоровье[$враг[i]]
 здоровье_врага[i] = базовое_здоровье[$враг[i]]
 выносливость_врага[i] = базовая_выносливость[$враг[i]]
 дух_врага[i] = базовый_дух[$враг[i]]
 вред_врага[i] = базовый_вред[$враг[i]]
 случай = RAND(1,3)
 if случай = 1: $защита[i] = 'блок'
 if случай = 2: $защита[i] = 'контратака'
 if случай = 3: $защита[i] = 'уклонение'
 враг[i] = 1
 jump 'loop'
END

текущий = 1
$суперудар[1] = $gt_spm[$g_type]

GS 'боевое_меню'
GT 'боевой_интерфейс'
--- $gladiator_fight_start ---------------------------------

