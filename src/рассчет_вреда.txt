# рассчет_вреда
!CALCULATION DAMAGE FROM ALL MODIFICATORS
модификатор = 0

!Interaction of the type of protection and attack
$эффект = 'удвоение'
if $защита[def] = 'блок' and $атака[atk] = 'сильная': модификатор += 1 
if $защита[def] = 'уклонение' and $атака[atk] = 'быстрая': модификатор += 1
if $защита[def] = 'контратака' and $атака[atk] = 'точная': модификатор += 1

if $защита[def] = 'блок' and $атака[atk] = 'точная': $эффект = 'бонус'
if $защита[def] = 'уклонение' and $атака[atk] = 'сильная': $эффект = 'бонус'
if $защита[def] = 'контратака' and $атака[atk] = 'быстрая': $эффект = 'бонус'

if $защита[def] = 'блок' and $атака[atk] = 'быстрая': $эффект = 'штраф'
if $защита[def] = 'уклонение' and $атака[atk] = 'точная': $эффект = 'штраф'
if $защита[def] = 'контратака' and $атака[atk] = 'сильная': $эффект = 'штраф'

!taking statuses into account
IF $атака[atk] = 'сильная':
 if бешенство[atk] > 0: модификатор += 1
 if шаткость[def] > 0: модификатор += 1
 if слабость[atk] > 0: модификатор -= 1
 if адреналин[def] > 0: модификатор -= 1
END

IF $атака[atk] = 'быстрая':
 if разгон[atk] > 0: модификатор += 1
 if ошеломление[def] > 0: модификатор += 1
 if скованность[atk] > 0: модификатор -= 1
 if верткость[def] > 0: модификатор -= 1
END

IF $атака[atk] = 'точная':
 if меткость[atk] > 0: модификатор += 1
 if рассеянность[def] > 0: модификатор += 1
 if боль[atk] > 0: модификатор -= 1
 if внимание[def] > 0: модификатор -= 1
END

!damage calculation itself
if модификатор < -2: нанесенный_вред[atk] = вред_на_рассчет/8
if модификатор = -2: нанесенный_вред[atk] = вред_на_рассчет/4
if модификатор = -1: нанесенный_вред[atk] = вред_на_рассчет/2
if модификатор = 0: нанесенный_вред[atk] = вред_на_рассчет
if модификатор = 1: нанесенный_вред[atk] = вред_на_рассчет*2
if модификатор = 2: нанесенный_вред[atk] = вред_на_рассчет*4
if модификатор > 2: нанесенный_вред[atk] = вред_на_рассчет*8

модифер = 0

IF def = 0: dynamic 'модифер = модификатор_<<$рассчетный_тип_вреда>>'
IF def > 0: dynamic 'модифер = модификатор_<<$рассчетный_тип_вреда>>[$враг[def]]'
нанесенный_вред[atk] += модифер

if нанесенный_вред[atk] < 1: нанесенный_вред[atk] = 1
$тип_нанесенного_вреда[atk] = $рассчетный_тип_вреда

!'
if $рассчетный_тип_вреда = 'колющий': нанесенный_вред[atk] += модификатор_колющий[def]
if $рассчетный_тип_вреда = 'рубящий': нанесенный_вред[atk] += модификатор_рубящий[def]
if $рассчетный_тип_вреда = 'дробящий': нанесенный_вред[atk] += модификатор_дробящий[def]
if $рассчетный_тип_вреда = 'огонь': нанесенный_вред[atk] += модификатор_огонь[def]
if $рассчетный_тип_вреда = 'холод': нанесенный_вред[atk] += модификатор_холод[def]
if $рассчетный_тип_вреда = 'молния': нанесенный_вред[atk] += модификатор_молния[def]
if $рассчетный_тип_вреда = 'тьма': нанесенный_вред[atk] += модификатор_тьма[def]
if $рассчетный_тип_вреда = 'свет': нанесенный_вред[atk] += модификатор_свет[def]
if $рассчетный_тип_вреда = 'серебро': нанесенный_вред[atk] += модификатор_серебро[def]
if $рассчетный_тип_вреда = 'холодное_железо': нанесенный_вред[atk] += модификатор_холодное_железо[def]
if $рассчетный_тип_вреда = 'психический': нанесенный_вред[atk] += модификатор_психический[def]'
--- рассчет_вреда ---------------------------------

